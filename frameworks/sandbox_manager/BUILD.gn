# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/idl_tool/idl.gni")
import("//build/ohos.gni")
import("../../sandbox_manager.gni")

idl_gen_interface("sandbox_manager_interface") {
  sources = [ "ISandboxManager.idl" ]
}

ohos_source_set("sandbox_manager_proxy") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  output_values = get_target_outputs(":sandbox_manager_interface")
  sources = filter_include(output_values, [ "*_proxy.cpp" ])
  public_configs =
      [ ":sandbox_manager_communication_adapter_cxx_public_config" ]
  deps = [ ":sandbox_manager_interface" ]
  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
    "samgr:samgr_proxy",
  ]
  subsystem_name = "accesscontrol"
  part_name = "sandbox_manager"
}

ohos_source_set("sandbox_manager_stub") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  output_values = get_target_outputs(":sandbox_manager_interface")
  sources = filter_include(output_values, [ "*_stub.cpp" ])
  public_configs =
      [ ":sandbox_manager_communication_adapter_cxx_public_config" ]
  deps = [ ":sandbox_manager_interface" ]
  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
    "samgr:samgr_proxy",
  ]
  subsystem_name = "accesscontrol"
  part_name = "sandbox_manager"
}

config("sandbox_manager_communication_adapter_cxx_public_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "${target_gen_dir}",
    "include",
    "${sandbox_manager_path}/frameworks/common/include",
    "${sandbox_manager_path}/interfaces/inner_api/sandbox_manager/include",
  ]
}

ohos_shared_library("sandbox_manager_communication_adapter_cxx") {
  subsystem_name = "accesscontrol"
  innerapi_tags = [ "platformsdk_indirect" ]
  part_name = "sandbox_manager"

  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"

  public_configs =
      [ ":sandbox_manager_communication_adapter_cxx_public_config" ]
  configs = [ "${sandbox_manager_path}/config:coverage_flags" ]

  include_dirs = [
    "${sandbox_manager_path}/interfaces/inner_api/sandbox_manager/include",
    "${sandbox_manager_path}/frameworks/common/include",
    "include",
  ]

  sources = [
    "src/policy_info_parcel.cpp",
    "src/policy_info_vector_parcel.cpp",
    "src/sandbox_manager_dfx_helper.cpp",
    "src/sandbox_manager_log.cpp",
  ]

  external_deps = [
    "c_utils:utils",
    "hisysevent:libhisysevent",
    "ipc:ipc_core",
  ]
}
